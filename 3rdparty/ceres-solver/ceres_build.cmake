include(ExternalProject)

set_local_or_remote_url(
    DOWNLOAD_URL_PRIMARY
    LOCAL_URL   "${THIRD_PARTY_DOWNLOAD_DIR}/ceres-solver-1.14.0.zip"
    REMOTE_URLS "https://github.com/ceres-solver/ceres-solver/archive/1.14.0.zip"
)

message("GLOG_CMAKE_FLAGS: " ${GLOG_CMAKE_FLAGS})
message("EIGEN_CMAKE_FLAGS: " ${EIGEN_CMAKE_FLAGS})
message("SUITESPARSE_CMAKE_FLAGS: " ${SUITESPARSE_CMAKE_FLAGS})

ExternalProject_Add(
   ext_ceres
   PREFIX ${CUSTOM_BUILD_DIR}
   URL ${DOWNLOAD_URL_PRIMARY} ${DOWNLOAD_URL_FALLBACK}
   URL_HASH MD5=26b255b7a9f330bbc1def3b839724a2a
   BUILD_IN_SOURCE 0
   BUILD_ALWAYS 0
   UPDATE_COMMAND ""
   SOURCE_DIR ${CMAKE_CURRENT_BINARY_DIR}/ceres
   BINARY_DIR ${CUSTOM_BUILD_DIR}/ceres_build
   INSTALL_DIR ${CUSTOM_INSTALL_DIR}
   CMAKE_ARGS
          ${EIGEN_CMAKE_FLAGS}
          ${GLOG_CMAKE_FLAGS}
          ${SUITESPARSE_CMAKE_FLAGS}
          -DBUILD_SHARED_LIBS=$<IF:$<PLATFORM_ID:Linux>,ON,OFF>
          -DCMAKE_BUILD_TYPE=$<IF:$<PLATFORM_ID:Windows>,${CMAKE_BUILD_TYPE},Release>
          -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
          -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
          -DGFLAGS=ON
          -DLAPACK=ON
          -DSUITESPARSE=ON
          -DOPENMP=ON
          -DBUILD_TESTING=OFF
          -DBUILD_EXAMPLES=OFF
          -DCMAKE_INSTALL_PREFIX:PATH=<INSTALL_DIR>
          $<$<PLATFORM_ID:Windows>:-DCMAKE_CXX_FLAGS=/DGOOGLE_GLOG_DLL_DECL=>
          DEPENDS ${INTERNAL_EIGEN3_TARGET} ${SUITESPARSE_TARGET} ${GLOG_TARGET}
)

ExternalProject_Get_Property(ext_ceres INSTALL_DIR)
set(CERES_INCLUDE_DIRS ${INSTALL_DIR}/include/) # "/" is critical.
set(CERES_LIB_DIR ${INSTALL_DIR}/lib)
if (WIN32)
    set(EXT_CERES_LIBRARIES ceres$<$<CONFIG:Debug>:-debug>)
else()
    set(EXT_CERES_LIBRARIES ceres)
endif()
set(CERES_CMAKE_FLAGS ${SUITESPARSE_CMAKE_FLAGS} ${EIGEN_CMAKE_FLAGS} ${GLOG_CMAKE_FLAGS} -DCeres_DIR=${CERES_LIB_DIR}/cmake/Ceres)
