include(ExternalProject)

if (NOT USE_SIMD)
    set(EIGEN_ALIGN_FLAGS
            "-DCMAKE_CXX_FLAGS:STRING=-DEIGEN_MAX_ALIGN_BYTES=0 -DEIGEN_MAX_STATIC_ALIGN_BYTES=0"
            "-DCMAKE_C_FLAGS:STRING=-DEIGEN_MAX_ALIGN_BYTES=0 -DEIGEN_MAX_STATIC_ALIGN_BYTES=0")
else ()
    set(EIGEN_ALIGN_FLAGS ${CLOUDVIEWERCONFIG_SSE_DEFINITIONS})
    if (MSVC)
        set(EIGEN_ALIGN_FLAGS ${EIGEN_ALIGN_FLAGS} -DEIGEN_TEST_SSE2=ON)
    endif ()
endif ()

ExternalProject_Add(ext_eigen
        PREFIX eigen
        # Commit point: https://gitlab.com/libeigen/eigen/-/merge_requests/716
        URL https://gitlab.com/libeigen/eigen/-/archive/da7909592376c893dabbc4b6453a8ffe46b1eb8e/eigen-da7909592376c893dabbc4b6453a8ffe46b1eb8e.tar.gz
        URL_HASH SHA256=37f71e1d7c408e2cc29ef90dcda265e2de0ad5ed6249d7552f6c33897eafd674
        DOWNLOAD_DIR "${CLOUDVIEWER_THIRD_PARTY_DOWNLOAD_DIR}/eigen"
        INSTALL_DIR ${CLOUDVIEWER_EXTERNAL_INSTALL_DIR}
        BUILD_IN_SOURCE 0
        BUILD_ALWAYS 0
        UPDATE_COMMAND ""
        CMAKE_ARGS
            -DCMAKE_POLICY_VERSION_MINIMUM=3.5
            ${EIGEN_ALIGN_FLAGS}
            # -DCMAKE_BUILD_TYPE=$<IF:$<PLATFORM_ID:Windows>,${CMAKE_BUILD_TYPE},Release>
            $<IF:$<PLATFORM_ID:Windows>,"",-DCMAKE_CXX_FLAGS=-D_GLIBCXX_USE_CXX11_ABI=${CUSTOM_GLIBCXX_USE_CXX11_ABI}>
            -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
            -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
            -DCMAKE_C_COMPILER_LAUNCHER=${CMAKE_C_COMPILER_LAUNCHER}
            -DCMAKE_CXX_COMPILER_LAUNCHER=${CMAKE_CXX_COMPILER_LAUNCHER}
            -DCMAKE_POSITION_INDEPENDENT_CODE=ON
            -DCMAKE_INSTALL_PREFIX:PATH=<INSTALL_DIR>)

ExternalProject_Get_Property(ext_eigen INSTALL_DIR)
set(EIGEN_INCLUDE_DIRS ${INSTALL_DIR}/include/eigen3/) # "/" is critical.
set(EIGEN_CMAKE_FLAGS ${EIGEN_ALIGN_FLAGS} ${MAC_OMP_FLAGS} -DEigen3_DIR:PATH=${INSTALL_DIR}/share/eigen3/cmake -DEIGEN3_INCLUDE_DIR=${EIGEN_INCLUDE_DIRS} -DEIGEN_INCLUDE_DIR=${EIGEN_INCLUDE_DIRS})


